plugins {
    id 'java-library'
    id 'eclipse'  // necessary for workaround below
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

sourceSets {
    examples {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    examplesImplementation.extendsFrom implementation
    examplesRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    // OkHttp 3 - Http client (without Kotlin dependency of version 4)
    api("com.squareup.okhttp3:okhttp:3.14.2")
    implementation("com.squareup.okhttp3:logging-interceptor:3.14.2")
    
    // Moshi JSON Library
    implementation("com.squareup.moshi:moshi:1.8.0")
    
    // Logging facade
    implementation("org.slf4j:slf4j-api:1.7.28")
    
    // jUnit 5 for Testing
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.5.1")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:5.5.1")
    
    testRuntimeOnly('org.apache.logging.log4j:log4j-slf4j-impl:2.12.1')
    
    // google api client for native auth example
    examplesImplementation('com.google.api-client:google-api-client:1.30.2')
}

test {
    useJUnitPlatform()
}

task runExample(type: JavaExec) {
    classpath = sourceSets.examples.runtimeClasspath
    main = project.hasProperty("example") ? project.getProperty("example") : "com.nylas.examples.Examples"
}

task createProperties(dependsOn: processResources) {
    doLast {
        new File("$buildDir/resources/main/version.properties").withWriter { w ->
            Properties p = new Properties()
            p['version'] = project.version.toString()
            p.store w, null
        }
    }
}

classes {
    dependsOn createProperties
}

// workaround for https://github.com/eclipse/buildship/issues/476
eclipse {
    classpath {
        plusConfigurations.add configurations.examplesCompileClasspath
    }
}